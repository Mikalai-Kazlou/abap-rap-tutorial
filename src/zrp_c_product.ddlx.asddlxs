@Metadata.layer: #CORE

@UI.headerInfo: { typeName: 'Product',
                  typeNamePlural: 'Products',
                  imageUrl: 'ProductGroupImageUrl',
                  title: { type: #STANDARD, label: 'Product', value: 'Name' } }

@UI.presentationVariant: [ { sortOrder: [ { by: 'ProductGroupName', direction: #ASC },
                                          { by: 'ID', direction: #ASC } ] } ]
@UI.lineItem: [{criticality: 'PhaseID'}]

@UI.chart: [{ qualifier: 'BulletChart',
              title: 'Bullet Micro Chart',
              description: 'Micro Chart of Net Amount In Gross Amount',
              chartType: #BULLET,
              measures: ['NetAmountInGrossAmount'],
              measureAttributes: [{ measure: 'NetAmountInGrossAmount',
                                    role: #AXIS_1,
                                    asDataPoint: true
                                 }]
           }]

annotate view ZRP_C_PRODUCT with
{
  @UI.facet: [

    { targetQualifier: 'Phase',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 10
    },
    { targetQualifier: 'ProductID',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 20
    },
    { targetQualifier: 'NetPrice',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 30
    },
    { targetQualifier: 'TaxRate',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 40
    },
    { targetQualifier: 'IncomePercentage',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 50
    },

    { id: 'GeneralData',
      label: 'General data',
      purpose: #STANDARD,
      type: #COLLECTION,
      targetQualifier: 'GeneralData',
      position: 10
    },
      { id: 'Product',
        label: 'Product',
        parentId: 'GeneralData',
        purpose: #STANDARD,
        type: #FIELDGROUP_REFERENCE,
        targetQualifier: 'Product',
        position: 10
      },
      { id: 'Price',
        label: 'Price',
        parentId: 'GeneralData',
        purpose: #STANDARD,
        type: #FIELDGROUP_REFERENCE,
        targetQualifier: 'Price',
        position: 20
      },
      { id: 'Dimensions',
        label: 'Dimensions',
        parentId: 'GeneralData',
        purpose: #STANDARD,
        type: #FIELDGROUP_REFERENCE,
        targetQualifier: 'Dimensions',
        position: 30
      },

    { id: 'TechnicalData',
      label: 'Technical data',
      purpose: #STANDARD,
      type: #FIELDGROUP_REFERENCE,
      targetQualifier: 'TechnicalData',
      position: 20
    },

    { id: 'ProductMarkets',
      type: #LINEITEM_REFERENCE,
      purpose: #STANDARD,
      label: 'Product Markets',
      targetElement: 'ProductMarkets',
      position: 30
    },

    { id: 'MarketQuantityChart',
      type: #CHART_REFERENCE,
      purpose: #STANDARD,
      label: 'Market Quantity Chart',
      targetQualifier: 'MarketQuantityChart',
      targetElement: 'ByMarketChart',
      position: 40
    },

    { id: 'MarketAmountChart',
      type: #CHART_REFERENCE,
      purpose: #STANDARD,
      label: 'Market Amount Chart',
      targetQualifier: 'MarketAmountChart',
      targetElement: 'ByMarketChart',
      position: 50
    }
  ]

  @UI.identification: [{
    type: #FOR_ACTION,
    dataAction: 'MoveToNextPhase',
    label: 'Next Phase'
  }]

  @UI.lineItem: [{
    type: #FOR_ACTION,
    dataAction: 'CopyProduct',
    label: 'Copy'
  },
  { type: #FOR_ACTION,
    dataAction: 'MoveToNextPhase',
    label: 'Next Phase'
  }]

  @UI.hidden: true
  UUID;

  @EndUserText.label: 'Product ID'
  @UI.dataPoint: { title: 'Product ID', qualifier: 'ProductID' }
  @UI.fieldGroup: [ { qualifier: 'Product', position: 20 } ]
  @UI.lineItem: [ { position: 10 } ]
  @UI.selectionField: [ { position: 10 } ]
  ID;

  @UI.fieldGroup: [ { qualifier: 'Product', position: 30 } ]
  @UI.lineItem: [ { position: 20 } ]
  @UI.selectionField: [ { position: 20 } ]
  Name;

  @EndUserText.label: 'Product Group'
  @UI.fieldGroup: [ { qualifier: 'Product', position: 40 } ]
  @UI.lineItem: [ { position: 30 } ]
  @UI.selectionField: [ { position: 30 } ]
  ProductGroupID;

  @UI.hidden: true
  ProductGroupName;

  @UI.hidden: true
  ProductGroupImageUrl;

  @EndUserText.label: 'Phase'
  @UI.dataPoint: { title: 'Phase', qualifier: 'Phase', criticality: 'PhaseID' }
  @UI.fieldGroup: [ { qualifier: 'Product', position: 10, criticality: 'PhaseID' } ]
  @UI.lineItem: [ { position: 40, criticality: 'PhaseID' } ]
  @UI.selectionField: [ { position: 40 } ]
  PhaseID;

  @UI.hidden: true
  PhaseText;

  @UI.fieldGroup: [ { position: 10, qualifier: 'Dimensions' },
                    { position: 10, qualifier: 'AllMeasures', value: 'Height' } ]
  Height;

  @UI.fieldGroup: [ { position: 20, qualifier: 'Dimensions' },
                    { position: 20, qualifier: 'AllMeasures', value: 'Depth' } ]
  Depth;

  @UI.fieldGroup: [ { position: 30, qualifier: 'Dimensions' },
                    { position: 30, qualifier: 'AllMeasures', value: 'Width' } ]
  Width;

  @UI.lineItem: [{
    position: 50,
    label: 'Size (HxDxW)',
    type: #AS_FIELDGROUP,
    valueQualifier: 'AllMeasures'
  }]

  @EndUserText.label: 'Size UoM'
  SizeUOM;

  @UI.dataPoint: { title: 'Net Price', qualifier: 'NetPrice' }
  @UI.fieldGroup: [ { qualifier: 'Price', position: 10 } ]
  @UI.lineItem: [ { position: 90 } ]
  @UI.selectionField: [ { position: 50 } ]
  Price;

  @UI.selectionField: [ { position: 60 } ]
  PriceCurrency;

  @UI.dataPoint: { title: 'Tax Rate', qualifier: 'TaxRate' }
  @UI.fieldGroup: [ { qualifier: 'Price', position: 20 } ]
  @UI.lineItem: [ { position: 110 } ]
  TaxRate;

  @UI.dataPoint: {
    title: 'Income Percentage KPI',
    qualifier: 'IncomePercentage',
    targetValue: 50,
    visualization: #PROGRESS,
    criticality: 'IncomePercentageCriticality'
  }
  @UI.lineItem: [{
    position: 120,
    label: 'Income Percentage KPI',
    type: #AS_DATAPOINT
  }]
  IncomePercentage;

  @UI.hidden: true
  IncomePercentageCriticality;

  @UI.dataPoint: {
    title: 'Net Amount In Gross Amount',
    qualifier: 'NetAmountInGrossAmount',
    targetValue: 70,
    maximumValue: 100,
    minimumValue: 0,
    criticalityCalculation: { improvementDirection: #MINIMIZE,
                              deviationRangeHighValue: 85,
                              toleranceRangeHighValue: 82,
                              toleranceRangeLowValue: 0
                            }
  }

  @UI.lineItem: [{
    position: 130,
    type: #AS_CHART,
    label: 'Net Amount In Gross Amount',
    valueQualifier: 'BulletChart',
    importance: #HIGH
  }]

  @EndUserText.label: 'Net Amount In Gross Amount'
  NetAmountInGrossAmount;

  @UI.fieldGroup: [{
    position: 10,
    qualifier: 'TechnicalData',
    type: #AS_CONTACT,
    value: 'CreatedByContact',
    label: 'Created By'
  }]
  @UI.lineItem: [{
    position: 910,
    type: #AS_CONTACT,
    value: 'CreatedByContact',
    label: 'Created By',
    importance: #LOW
  }]
  CreatedBy;

  @UI.fieldGroup: [ { qualifier: 'TechnicalData', position: 20 } ]
  @UI.lineItem: [ { position: 920, importance: #LOW } ]
  CreatedOn;

  @UI.fieldGroup: [{
    position: 30,
    qualifier: 'TechnicalData',
    type: #AS_CONTACT,
    value: 'ChangedByContact',
    label: 'Changed By'
  }]
  @UI.lineItem: [{
    position: 930,
    type: #AS_CONTACT,
    value: 'ChangedByContact',
    label: 'Changed By',
    importance: #LOW
  }]
  ChangedBy;

  @UI.fieldGroup: [ { qualifier: 'TechnicalData', position: 40 } ]
  @UI.lineItem: [ { position: 940, importance: #LOW } ]
  ChangedOn;

  @UI.hidden: true
  LocalChangedOn;
}