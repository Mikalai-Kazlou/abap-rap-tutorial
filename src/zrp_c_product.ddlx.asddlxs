@Metadata.layer: #CORE
@UI.headerInfo: { typeName: 'Product',
                  typeNamePlural: 'Products',
                  imageUrl: 'ProductGroupImageUrl',
                  title: { type: #STANDARD, label: 'Product', value: 'Name' } }

@UI.presentationVariant: [ { sortOrder: [ { by: 'ProductGroupName', direction: #ASC },
                                          { by: 'ID', direction: #ASC } ] } ]

@UI.lineItem: [{criticality: 'PhaseID'}]

annotate view ZRP_C_PRODUCT with
{
  @UI.facet: [

    { targetQualifier: 'Phase',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 10
    },
    { targetQualifier: 'ProductID',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 20
    },
    { targetQualifier: 'NetPrice',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 30
    },
    { targetQualifier: 'TaxRate',
      type: #DATAPOINT_REFERENCE,
      purpose: #HEADER,
      position: 40
    },

    { id: 'GeneralData',
      label: 'General data',
      purpose: #STANDARD,
      type: #COLLECTION,
      targetQualifier: 'GeneralData',
      position: 10
    },
      { id: 'Product',
        label: 'Product',
        parentId: 'GeneralData',
        purpose: #STANDARD,
        type: #FIELDGROUP_REFERENCE,
        targetQualifier: 'Product',
        position: 10
      },
      { id: 'Price',
        label: 'Price',
        parentId: 'GeneralData',
        purpose: #STANDARD,
        type: #FIELDGROUP_REFERENCE,
        targetQualifier: 'Price',
        position: 20
      },
      { id: 'Dimensions',
        label: 'Dimensions',
        parentId: 'GeneralData',
        purpose: #STANDARD,
        type: #FIELDGROUP_REFERENCE,
        targetQualifier: 'Dimensions',
        position: 30
      },

    { id: 'TechnicalData',
      label: 'Technical data',
      purpose: #STANDARD,
      type: #FIELDGROUP_REFERENCE,
      targetQualifier: 'TechnicalData',
      position: 20
    },

    { type: #LINEITEM_REFERENCE,
      purpose: #STANDARD,
      label: 'Product Markets',
      id: 'ProductMarkets',
      targetElement: 'ProductMarkets',
      position: 30
    },

    { id: 'MarketQuantityChart',
      type: #CHART_REFERENCE,
      purpose: #STANDARD,
      label: 'Market Quantity Chart',
      targetQualifier: 'MarketQuantityChart',
      targetElement: 'ByMarketChart',
      position: 40
    },

    { id: 'MarketAmountChart',
      type: #CHART_REFERENCE,
      purpose: #STANDARD,
      label: 'Market Amount Chart',
      targetQualifier: 'MarketAmountChart',
      targetElement: 'ByMarketChart',
      position: 50
    }
  ]

  @UI.hidden: true
  UUID;

  @EndUserText.label: 'Product ID'
  @UI.dataPoint: { title: 'Product ID', qualifier: 'ProductID' }
  @UI.fieldGroup: [ { qualifier: 'Product', position: 20 } ]
  @UI.lineItem: [ { position: 10 },
                  { type: #FOR_ACTION, dataAction: 'CopyProduct', label: 'Copy' } ]
  @UI.selectionField: [ { position: 10 } ]
  ID;

  @UI.fieldGroup: [ { qualifier: 'Product', position: 30 } ]
  @UI.lineItem: [ { position: 20 } ]
  @UI.selectionField: [ { position: 20 } ]
  Name;

  @EndUserText.label: 'Product Group'
  @UI.fieldGroup: [ { qualifier: 'Product', position: 40 } ]
  @UI.lineItem: [ { position: 30 } ]
  @UI.selectionField: [ { position: 30 } ]
  ProductGroupID;

  @UI.hidden: true
  ProductGroupName;

  @UI.hidden: true
  ProductGroupImageUrl;

  @EndUserText.label: 'Phase'
  @UI.dataPoint: { title: 'Phase', qualifier: 'Phase', criticality: 'PhaseID' }
  @UI.fieldGroup: [ { qualifier: 'Product', position: 10, criticality: 'PhaseID' } ]
  @UI.identification: [ { type: #FOR_ACTION, dataAction: 'MoveToNextPhase', label: 'Next Phase' } ]
  @UI.lineItem: [ { position: 40, criticality: 'PhaseID' },
                  { type: #FOR_ACTION, dataAction: 'MoveToNextPhase', label: 'Next Phase' } ]
  @UI.selectionField: [ { position: 40 } ]
  PhaseID;

  @UI.hidden: true
  PhaseText;

  @UI.fieldGroup: [ { qualifier: 'Dimensions', position: 10 } ]
  @UI.lineItem: [ { position: 50 } ]
  Height;

  @UI.fieldGroup: [ { qualifier: 'Dimensions', position: 20 } ]
  @UI.lineItem: [ { position: 60 } ]
  Depth;

  @UI.fieldGroup: [ { qualifier: 'Dimensions', position: 30 } ]
  @UI.lineItem: [ { position: 70 } ]
  Width;

  @EndUserText.label: 'Size UoM'
  SizeUOM;

  @UI.dataPoint: { title: 'Net Price', qualifier: 'NetPrice' }
  @UI.fieldGroup: [ { qualifier: 'Price', position: 10 } ]
  @UI.lineItem: [ { position: 90 } ]
  @UI.selectionField: [ { position: 50 } ]
  Price;

  @UI.selectionField: [ { position: 60 } ]
  PriceCurrency;

  @UI.dataPoint: { title: 'Tax Rate', qualifier: 'TaxRate' }
  @UI.fieldGroup: [ { qualifier: 'Price', position: 20 } ]
  @UI.lineItem: [ { position: 110 } ]
  TaxRate;

  @UI.fieldGroup: [ { qualifier: 'TechnicalData', position: 10 } ]
  @UI.lineItem: [ { position: 120 } ]
  CreatedBy;

  @UI.fieldGroup: [ { qualifier: 'TechnicalData', position: 20 } ]
  @UI.lineItem: [ { position: 130 } ]
  CreatedOn;

  @UI.fieldGroup: [ { qualifier: 'TechnicalData', position: 30 } ]
  @UI.lineItem: [ { position: 140 } ]
  ChangedBy;

  @UI.fieldGroup: [ { qualifier: 'TechnicalData', position: 40 } ]
  @UI.lineItem: [ { position: 150 } ]
  ChangedOn;
}